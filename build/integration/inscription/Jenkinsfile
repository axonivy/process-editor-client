pipeline {
  agent any

  options {
    buildDiscarder(logRotator(numToKeepStr: '30', artifactNumToKeepStr: '5'))
    disableConcurrentBuilds()
  }

  triggers {
    cron '@midnight'
  }

  parameters {
    string(name: 'engineSource', defaultValue: 'https://product.ivyteam.io/', description: 'Engine page url')
    choice(name: 'browser', choices: ['', '*', 'chrome', 'firefox', 'webkit'], description: 'Browser used for integration tests (default: "all" on master/release, "chrome" on branch)')
  }

  stages {
    stage('Inscription Integration Test') {
      steps {
        script {
          docker.build('node-webtest', '-f build/Dockerfile.playwright .').inside {
            sh 'npm run update:axonivy:next && npm install'
            sh 'npm run package'
            dir ('playwright/inscription-test-project') {
              maven cmd: "-ntp verify -Dengine.page.url=${params.engineSource} -Dwebtest.cmd=webtest:inscription:${browser()}"
            }
          }
          archiveArtifacts artifacts: 'playwright/test-results/**', allowEmptyArchive: true
          archiveArtifacts artifacts: 'playwright/inscription-test-project/target/**/ivy.log', allowEmptyArchive: true
          withChecks('Inscription Integration WebTest') {
            junit testDataPublishers: [[$class: 'StabilityTestDataPublisher']], testResults: 'node_modules/**/report.xml'
          }
        }
      }
    }
    stage('Protocol') {
      when {
        expression { isReleasingBranch() }
      }
      steps {
        script {
          catchError(buildResult: 'UNSTABLE', stageResult: 'UNSTABLE') {
            docker.build('node', '-f build/Dockerfile.node .').inside {
              def branchName = env.BRANCH_NAME.replace("/", "%252F")
              sh '''
                npm install
                npm run protocol:inscription clean

                artifacts="https://jenkins.ivyteam.io/job/core_json-schema/job/'''+branchName+'''/lastSuccessfulBuild/artifact"
                devSchema="${artifacts}/workspace/ch.ivyteam.ivy.inscription.lsp.schema/target/schema/process/*/inscription.json/*zip*/process.zip"
                curl -o process.zip ${devSchema}
                unzip -j process.zip
                npm run protocol:inscription generate $PWD/inscription.json

                npm run build && npm run lint:ci
              '''
            }
          }
          withChecks('Integration Protocol ESLint') {
            recordIssues enabledForFailure: true, publishAllIssues: true, aggregatingResults: true, tools: [esLint(id: 'latest', pattern: 'packages/**/eslint.xml,integrations/**/eslint.xml')], qualityGates: [[threshold: 1, type: 'TOTAL', unstable: true]]
          }
        }
      }
    }
  }
}

def browser() {
  if (params.browser == '') {
    if (isReleasingBranch()) {
      return '*'
    }
    return 'chrome'
  }
  return params.browser;
}
